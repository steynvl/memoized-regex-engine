// EBNF to Railroad Diagrams: https://www.bottlecaps.de/rr/ui

regex ::= alternative

alternative ::= concatenation ('|' concatenation)*

concatenation ::= repetition repetition*

repetition ::= term ('*' | '+' | '?' | repetitionMinMax)*

repetitionMinMax ::= '{' INTEGER (',' INTEGER)? '}'

term ::= '^' | '$' | '.' | CHAR | group | '(?=' regex ')' | '(?!' regex ')' | '(' regex ')'

group ::= '[' '^'? (characterRange | CHAR)+ ']'

characterRange ::= CHAR '-' CHAR

<?TOKENS?>

CHAR ::= [^(){}^$*+?|.#x5b#x5d] |
    '\' ('[' | ']' | '(' | ')' | '{' | '}' | '^' | '$' | '*' | '+' | '?' | '.' | '|' | '\')

INTEGER ::= [0-9]+
